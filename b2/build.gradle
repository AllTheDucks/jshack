apply plugin: 'base'
apply plugin: 'war'


repositories {
    mavenCentral()
    maven {
        url "https://bbprepo.blackboard.com/content/repositories/releases/"
    }
}

configurations {
    b2deploy
}

war {
    dependsOn ":jshack-common-web:plovrBuild"
    from '../common-web/src/main/webapp'
    from '../common-web/build/plovr/compiled'
}

dependencies {

    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.mockito:mockito-all:1.9.5'

    b2deploy 'org.oscelot:b2deploy-task:0.1.0'

    compile project(':jshack-common-web')

    providedCompile 'blackboard.platform:bb-platform:9.1.100401.0'
    providedCompile 'blackboard.platform:bb-cms-admin:9.1.100401.0'
    providedCompile 'blackboard.platform:bb-taglibs:9.1.100401.0'
    providedCompile 'blackboard.platform:bb-ws-gradebook:9.1.100401.0'

    providedCompile 'commons-codec:commons-codec:1.3'
    providedCompile 'commons-io:commons-io:1.4'
    providedCompile 'commons-lang:commons-lang:2.4'
    providedCompile 'org.apache.velocity:velocity:1.5'

    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'jstl:jstl:1.2'
    providedCompile 'javax.activation:activation:1.1'


}

ant.taskdef(name: 'b2deploy', classname: 'org.oscelot.ant.B2DeployTask', classpath: configurations.b2deploy.asPath)

task deployb2(dependsOn: 'war') << {
    println "Deploying \"" + war.archivePath + "\""
    ant.b2deploy(localfilepath: war.archivePath,
        host: 'localhost:9876',
        clean: 'true',
        courseorgavailable:'true')
}

